set $DEBUG = 0

set print repeats 0
set print null-stop

file main.elf

set $prevTime = 0
define printContextInfo
	set $currTime = myTraceGetTimeMillisecond()
	
	if($currTime <= $prevTime)
		# In 'xPortSysTickHandler', 'NVIC_PENDSV' is set before 
		# calling 'vTaskIncrementTick' so the program is breaked by GDB
		# before xTickCount increasing.
		set $currTime = $currTime + 10
	end
	
	output /u $currTime
	
	if($DEBUG)
		echo ;
		output /u xTickCount * 10
		echo ;
		output /f myTraceGetTick() * 10
	end
	echo ,
	output /a pxCurrentTCB
	echo \n
	
	set $prevTime = $currTime
end

b myTraceCreate
commands 1
	silent
	echo %create,
	output /a ((tskTCB *)$r0)
	echo  ,
	output ((tskTCB *)$r0)->pcTaskName
	echo \n
	c
end

b myTraceSwitchedOut
commands 2
	silent
	echo %out,
	printContextInfo
	c
end

b myTraceSwitchedIn
commands 3
	silent
	echo %in,
	printContextInfo
	c
end

set logging on
target remote :3333
c
